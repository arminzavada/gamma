package target

import "interfaces"
import "activities"
import "dispatchers"
 
scheduled-async System {
	component messageQueue : MessageQueue
	component target : Target
	component dispatcher : Dispatcher
	
	// Hooking up MessageQueue
	channel [ dispatcher.queueControl ] -o)- [ messageQueue.queueControl ]
	channel [ messageQueue.data ] -o)- [ dispatcher.inputData ]
	
	// Hooking up target
	channel [ dispatcher.outputData ] -o)- [ target.data ]
	channel [ target.dispatcherControl ] -o)- [ dispatcher.dispatcherControl ]
	
	// Initialize by executing the Dispatcher component
	initial execute dispatcher
} 

@Asynchronous
statechart MessageQueue [
	port queueControl : requires QueueControl
	
	port data : provides Data
] {
	
	region Main {
		initial Initial
		state _0
		state _1
		state _2
		state _3
		state _4
	}
	
	transition from Initial to _0
	transition from _0 to _1 when queueControl.next / raise data.start;
	transition from _1 to _2 when queueControl.next / raise data.continue;
	transition from _2 to _3 when queueControl.next / raise data.continue;
	transition from _3 to _4 when queueControl.next / raise data.continue;
	
}

@RegionSchedule=bottom-up
statechart TargetSC [
	port data : requires Data
	
	port dispatcherControl : provides DispatcherControl
] {	
	
	region Wrapper {
		initial WrapperInitial
		state WrapperFunction {
			region Main {
				initial Initial
				state wait
				state S1 {
					exit / log "S1_exit";
					region S1Region {
						initial S1Initial
						state S1_1
						state S1_2 {
							exit / log "S1_2_exit";
						}
					}
					region S2Region {
						initial S2Initial
						state S2_1 {
							exit / log "S2_1_exit";
							region S2_1Region {
								initial S2_1Initial
								state S2_1_1
								state S2_1Final
							}
							region S2_2Region {
								initial S2_2Initial
								state S2_2_1
								state S2_2Final
							}
						}
						state S2_2
					}
				}
				state Final
			}
		}
	}
	
	transition from WrapperInitial to WrapperFunction
	
	@Internal
	transition from WrapperFunction to WrapperFunction when data.any / raise dispatcherControl.ignore; // default ignore, without exit-entry
	
	transition "T1" from Initial to wait
	transition "T2" from wait to S1 when data.start / raise dispatcherControl.ready;
	
	transition "T1_1" from S1Initial to S1_1
	transition "T1_2" from S1_1 to S1_2 when data.continue / log "T1_2_effect"; raise dispatcherControl.ready;
	transition "T1_3" from S1_1 to S1_2 when data.continue / log "T1_3_effect"; raise dispatcherControl.ready;
	
	transition "T2_1" from S2Initial to S2_1
	transition "T2_2" from S2_1 to S2_2 when data.continue / log "T2_2_effect"; raise dispatcherControl.ready;
	transition "T2_3" from S2_1 to S2_2 when data.continue / log "T2_3_effect"; raise dispatcherControl.ready;
	transition "T2_4" from S2_1 to S2_2 when data.continue / log "T2_4_effect"; raise dispatcherControl.ready;
	
	transition "T2_1_1" from S2_1Initial to S2_1_1
	transition "T2_1_2" from S2_1_1 to S2_1Final when data.continue / log "T2_1_2_effect"; raise dispatcherControl.ready;
		
	transition "T2_2_1" from S2_2Initial to S2_2_1
	transition "T2_2_2" from S2_2_1 to S2_2Final when data.continue / log "T2_2_2_effect"; raise dispatcherControl.ready;
	
	transition from S1 to Final when data.continue / raise dispatcherControl.ready;
}

adapter Target of component target : TargetSC {
	when any / run
	
	queue dataQueue(priority = 1, capacity = QUEUE_SIZE) {
		data.any
	}
}

package target

import "interfaces"
import "activities"
import "dispatchers"
 
scheduled-async System {
	component messageQueue : MessageQueue
	component target : Target
	component dispatcher : Dispatcher
	
	// Hooking up MessageQueue
	channel [ dispatcher.queueControl ] -o)- [ messageQueue.queueControl ]
	channel [ messageQueue.data ] -o)- [ dispatcher.inputData ]
	
	// Hooking up target
	channel [ dispatcher.outputData ] -o)- [ target.data ]
	channel [ target.dispatcherControl ] -o)- [ dispatcher.dispatcherControl ]
	
	// Initialize by executing the Dispatcher component
	initial execute dispatcher
} 

@Asynchronous
statechart MessageQueue [
	port queueControl : requires QueueControl
	
	port data : provides Data
] {
	
	region Main {
		initial Initial
		state _0
		state _1
		state _2
		state _3
		state _4
	}
	
	transition from Initial to _0
	transition from _0 to _1 when queueControl.next / raise data.start;
	transition from _1 to _2 when queueControl.next / raise data.anotherSignal;
	transition from _2 to _3 when queueControl.next / raise data.anotherSignal;
	transition from _3 to _4 when queueControl.next / raise data.continue;
	
}

@RegionSchedule=bottom-up
statechart TargetSC [
	port data : requires Data
	
	port dispatcherControl : provides DispatcherControl
] {	
	
	region Wrapper {
		initial WrapperInitial
		state WrapperFunction {
			region Main {
				initial Initial
				state Waiting {
					exit / log "Waiting_exit";
				}
				state S1 {
					entry / log "S1_entry";
					exit / log "S1_exit";
				}
				state Final
			}
		}
	}
	
	transition from WrapperInitial to WrapperFunction
	
	@Internal
	transition from WrapperFunction to WrapperFunction when data.any / raise dispatcherControl.ignore; // default ignore, without exit-entry
	
	transition from Initial to Waiting
	transition "T1" from Waiting to S1 when data.start / raise dispatcherControl.ready;
	
	@Internal
	transition "IT" from S1 to S1 when data.anotherSignal / log "IT_effect"; raise dispatcherControl.ready;
	
	transition "T2" from S1 to Final when data.continue / raise dispatcherControl.ready;
	
}

adapter Target of component target : TargetSC {
	when any / run
	
	queue dataQueue(priority = 1, capacity = QUEUE_SIZE) {
		data.any
	}
}

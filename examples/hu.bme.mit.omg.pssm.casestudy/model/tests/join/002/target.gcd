package target

import "interfaces"
import "activities"
import "dispatchers"
 
scheduled-async System {
	
	component eventQueue : EventQueue
	component target : Target
	component dispatcher : Dispatcher
	
	// Tester controlling - next, end
	channel [ dispatcher.queueControl ] -o)- [ eventQueue.queueControl ]
	
	// Actual target data propagation
	channel [ eventQueue.data ] -o)- [ dispatcher.inputData ]
	channel [ dispatcher.outputData ] -o)- [ target.data ]
	
	// Scheduler controlling - ready, ignore
	channel [ target.dispatcherControl ] -o)- [ dispatcher.dispatcherControl ]

	initial execute dispatcher

}

@Asynchronous
statechart EventQueue [
	port queueControl : requires QueueControl
	
	port data : provides Data
] {
	
	region Main {
		initial Initial
		state _0
		state _1
	}
	
	transition from Initial to _0
	transition from _0 to _1 when queueControl.next / raise data.start;
	
}

@RegionSchedule=bottom-up
statechart TargetSC [
	port data : requires Data
	
	port dispatcherControl : provides DispatcherControl
	
	port completion_s1_1_1 : provides StatechartCompletion
	port completion_s1_1_2 : provides StatechartCompletion
	port completion_s2 : provides StatechartCompletion
] {	
	
	region Wrapper {
		initial WrapperInitial
		state WrapperFunction {
			region Main {
				initial Initial
				state wait
				state S1 {
					region S1Region {
						initial S1Initial
						state S1_1 {
							region S1_1_1Region {
								initial S1_1_1Initial
								state S1_1_1 {
									entry / raise completion_s1_1_1.completion;
								}
							}
							region S1_1_2Region {
								initial S1_1_2Initial
								state S1_1_2 {
									entry / raise completion_s1_1_2.completion;
								}
							}
						}
						join Join
					}
				}
				state S2 {
					entry / raise completion_s2.completion;
				}
				
				state Final
			}
		}
	}
	
	transition from WrapperInitial to WrapperFunction
	
	@Internal
	transition from WrapperFunction to WrapperFunction when data.any / raise dispatcherControl.ignore; // default ignore, without exit-entry
	
	transition "T1" from Initial to wait
	transition "T2" from wait to S1_1 when data.start / raise dispatcherControl.ready;

	transition from S1Initial to S1_1

	transition "T1_1" from S1_1_1Initial to S1_1_1
	transition "T2_1" from S1_1_2Initial to S1_1_2
	
	transition "T1_2" from S1_1_1 to Join when completion_s1_1_1.completion
	transition "T2_2" from S1_1_2 to Join when completion_s1_1_2.completion
	
	transition "T3" from Join to S2
	transition "T4" from S2 to Final when completion_s2.completion
}

adapter Target of component target : TargetSC {
	when any / run
	
	queue completionQueue(priority = 2, capacity = QUEUE_SIZE) {
		completion_s1_1_1.any,
		completion_s1_1_2.any,
		completion_s2.any
	}
	queue dataQueue(priority = 1, capacity = QUEUE_SIZE) {
		data.any
	}
}

package target

import "interfaces"
import "activities"
import "dispatchers"
 
scheduled-async System {
	
	component eventQueue : EventQueue
	component target : Target
	component dispatcher : Dispatcher
	
	// Tester controlling - next, end
	channel [ dispatcher.queueControl ] -o)- [ eventQueue.queueControl ]
	
	// Actual target data propagation
	channel [ eventQueue.data ] -o)- [ dispatcher.inputData ]
	channel [ dispatcher.outputData ] -o)- [ target.data ]
	
	// Scheduler controlling - ready, ignore
	channel [ target.dispatcherControl ] -o)- [ dispatcher.dispatcherControl ]

	initial execute dispatcher

}

@Asynchronous
statechart EventQueue [
	port queueControl : requires QueueControl
	
	port data : provides Data
] {
	
	region Main {
		initial Initial
		state _0
		state _1
		state _2
	}
	
	transition from Initial to _0
	transition from _0 to _1 when queueControl.next / raise data.start;
	transition from _1 to _2 when queueControl.next / raise data.continue;
	
}

@RegionSchedule=bottom-up
statechart TargetSC [
	port data : requires Data
	
	port dispatcherControl : provides DispatcherControl
	
	port completion_s1_1 : provides StatechartCompletion
	port completion_s1_1_1 : provides StatechartCompletion
] {	
	
	region Wrapper {
		initial WrapperInitial
		state WrapperFunction {
			region Main1 {
				initial Initial1
				state S1_1 {
					region S1_1Region {
						initial S1_1Initial
						state S1_1_1 {
							entry / raise completion_s1_1_1.completion;
						}
						state S1_1Final {
							entry / raise completion_s1_1.completion;
						}
					}
				}
				state Final1
			}
			region Main2 {
				initial Initial2
				state S2_1
				state Final2
			}
		}
	}
	
	transition from WrapperInitial to WrapperFunction
	
	@Internal
	transition from WrapperFunction to WrapperFunction when data.any / raise dispatcherControl.ignore; // default ignore, without exit-entry
	
	transition from Initial1 to S1_1
	transition from S1_1Initial to S1_1_1
	transition from S1_1_1 to S1_1Final when completion_s1_1_1.completion
	transition from S1_1_1 to Final1 when completion_s1_1.completion
	
	transition from Initial2 to S2_1
	transition from S2_1 to Final2 when data.continue / raise dispatcherControl.ready;
	
}

adapter Target of component target : TargetSC {
	when any / run
	
	queue completionQueue(priority = 2, capacity = QUEUE_SIZE) {
		completion_s1_1.any,
		completion_s1_1_1.any
	}
	queue dataQueue(priority = 1, capacity = QUEUE_SIZE) {
		data.any
	}
}

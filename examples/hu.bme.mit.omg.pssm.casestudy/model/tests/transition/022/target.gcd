package target

import "interfaces"
import "activities"
import "dispatchers"
 
scheduled-async System {
	
	component eventQueue : EventQueue
	component target : Target
	component dispatcher : Dispatcher
	
	// Tester controlling - next, end
	channel [ dispatcher.queueControl ] -o)- [ eventQueue.queueControl ]
	
	// Actual target data propagation
	channel [ eventQueue.data ] -o)- [ dispatcher.inputData ]
	channel [ dispatcher.outputData ] -o)- [ target.data ]
	
	// Scheduler controlling - ready, ignore
	channel [ target.dispatcherControl ] -o)- [ dispatcher.dispatcherControl ]

	initial execute dispatcher

}

@Asynchronous
statechart EventQueue [
	port queueControl : requires QueueControl
	
	port data : provides Data
] {
	
	region Main {
		initial Initial
		state _0
		state _1
	}
	
	transition from Initial to _0
	transition from _0 to _1 when queueControl.next / raise data.start;
	
}

@RegionSchedule=bottom-up
statechart TargetSC [
	port data : requires Data
	
	port dispatcherControl : provides DispatcherControl
	
	port completion_incrementing : provides StatechartCompletion
] {	
	var count : integer := 0
	
	region Wrapper {
		initial WrapperInitial
		state WrapperFunction {
			region Main {
				initial Initial
				state Waiting
				state Incrementing {
					entry / raise completion_incrementing.completion;
				}
				state Final
			}
		}
	}
	
	transition from WrapperInitial to WrapperFunction
	
	@Internal
	transition from WrapperFunction to WrapperFunction when data.any / raise dispatcherControl.ignore; // default ignore, without exit-entry
	
	transition "T1" from Initial to Waiting
	transition "T2" from Waiting to Incrementing when data.start / raise dispatcherControl.ready;
	transition "T3" from Incrementing to Incrementing when completion_incrementing.completion [count < 5] / count := count + 1;
	transition "T4" from Incrementing to Final when completion_incrementing.completion [count == 5]
		
}

adapter Target of component target : TargetSC {
	when any / run
	
	queue completionQueue(priority = 2, capacity = QUEUE_SIZE) {
		completion_incrementing.any
	}
	queue dataQueue(priority = 1, capacity = QUEUE_SIZE) {
		data.any
	}
}

package target

import "interfaces"
import "activities"
import "dispatchers"
 
scheduled-async System {
	
	component eventQueue : EventQueue
	component target : Target
	component dispatcher : Dispatcher
	
	// Tester controlling - next, end
	channel [ dispatcher.queueControl ] -o)- [ eventQueue.queueControl ]
	
	// Actual target data propagation
	channel [ eventQueue.data ] -o)- [ dispatcher.inputData ]
	channel [ dispatcher.outputData ] -o)- [ target.data ]
	
	// Scheduler controlling - ready, ignore
	channel [ target.dispatcherControl ] -o)- [ dispatcher.dispatcherControl ]

	initial execute dispatcher

}

@Asynchronous
statechart EventQueue [
	port queueControl : requires QueueControl
	
	port data : provides Data
] {
	
	region Main {
		initial Initial
		state _0
		state _1
		state _2
		state _3
	}
	
	transition from Initial to _0
	transition from _0 to _1 when queueControl.next / raise data.start;
	transition from _1 to _2 when queueControl.next / raise data.anotherSignal;
	transition from _2 to _3 when queueControl.next / raise data.continue;
	
}

@RegionSchedule=bottom-up
statechart TargetSC [
	port data : requires Data
	
	port dispatcherControl : provides DispatcherControl
	
	port completion_s1 : provides StatechartCompletion
	port completion_s1_1 : provides StatechartCompletion
	port completion_s1_2 : provides StatechartCompletion
	port completion_s1_2_2 : provides StatechartCompletion
] {	
	
	region Wrapper {
		initial WrapperInitial
		state WrapperFunction {
			region Main {
				initial Initial
				state wait
				state S1 {
					region S1Region {
						initial S1Initial
						deep history S1Deep
						state S1_1  {
							entry / raise completion_s1_1.completion;
						}
						state S1_2 {
							region S1_2Region {
								initial S1_2Initial
								state S1_2_1
								state S1_2_2 {
									entry / raise completion_s1_2_2.completion;
								}
								state S1_2Final{
									entry / raise completion_s1_2.completion;
								}
							}
						}
						state S1Final {
							entry / raise completion_s1.completion;
						}
					}
				}
				state Final
			}
		}
	}
	
	transition from WrapperInitial to WrapperFunction
	
	@Internal
	transition from WrapperFunction to WrapperFunction when data.any / raise dispatcherControl.ignore; // default ignore, without exit-entry
	
	transition "T1" from Initial to wait
	transition "T2" from wait to S1Deep when data.start / raise dispatcherControl.ready;
		
	transition "T1_1" from S1Initial to S1_1
	transition "T1_2" from S1_1 to S1_2 when completion_s1_1.completion
	transition "T1_2_1" from S1_2Initial to S1_2_1
	transition "T1_2_2" from S1_2_1 to S1_2_2 when data.continue / raise dispatcherControl.ready;
	transition "T1_2_3" from S1_2_2 to S1_2Final when completion_s1_2_2.completion
	transition "T1_3" from S1_2 to S1Final when completion_s1_2.completion
	transition "T1_4" from S1Deep to S1_2
	
	transition "T3" from S1 to S1Deep when data.anotherSignal / raise dispatcherControl.ready;
	transition "T4" from S1 to Final when completion_s1.completion
	
}

adapter Target of component target : TargetSC {
	when any / run
	
	queue completionQueue(priority = 2, capacity = QUEUE_SIZE) {
		completion_s1.any,
		completion_s1_1.any,
		completion_s1_2.any,
		completion_s1_2_2.any
	}	
	queue dataQueue(priority = 1, capacity = QUEUE_SIZE) {
		data.any
	}
}
